/*
    On modélise maintenant l'avancement d'un front de liquide dans le milieu,
    le problème est celui de l'imprégnation par un fluide d'un milieu poreux 
    initialement sec, rempli d'un fluide qui est l'air, de viscosité nulle.

        A l'exécution, on observe que le front se bloque sur les bords.

    auteur : Julien VALENTIN
    date   : 13 mai 2022

    execution dans un terminal externe
    ----------------------------------
    FreeFem++ "C:\Users\julien\Desktop\scikit\fr\physiques\darcy\FreeFem\03_darcy_evolution.edp" -ns

*/

// Géométrie : cavité rectangulaire
real largeur = 1;
real hauteur = 5;
mesh Th = square(10, 100, [largeur*x, hauteur*y]);
plot(Th, cmm="cavite", wait=true);

// Definition du cadre variationnel
fespace Qh(Th, P1);
Qh ph, qh;

fespace Vh(Th, P1b);
Vh uh, vh;

fespace Wh(Th, P2);
Wh phih;

/*
    Matériau homogène : mu est constant en espace
    Matériau isotrope : K = K Id et constant en espace

    Pour cette démonstration, les coefficients sont normalisés.
*/
real mu = 1;
real  K = 1;

// Probleme variationnel
macro grad(u)[dx(u), dy(u)]//
solve darcy(ph, qh) = int2d(Th)( grad(ph)'*grad(qh) ) + on(3, ph=1) + on(1, ph=0);

uh = K/mu*dx(ph);
vh = K/mu*dy(ph);

plot(ph, cmm="pression", value=true, wait=true);
plot(uh, cmm="vitesse horizontale", value=true, wait=true);
plot(vh, cmm="vitesse verticale", value=true, wait=true);
plot([uh,vh], cmm="vitesse", value=true, wait=true);

// Initialisation de la ligne de front
func phi0 = y < hauteur/10 ? 1 : 0;

phih = phi0;

real  t = 0;
real tf = 50;
real dt = 0.1;

for(int i=0; i < tf/dt; i++){
    t += dt;
    phih = convect([uh, vh], -dt, phih);
    plot(phih, cmm="Front a t = "+t, fill=true, value=true, wait=true);
}