/*
    On modélise maintenant l'avancement d'un front de liquide dans le milieu,
    le problème est celui de l'imprégnation par un fluide d'un milieu poreux 
    initialement sec, rempli d'un fluide qui est l'air, de viscosité nulle.

    auteur : Julien VALENTIN
    date   : 13 mai 2022

    execution dans un terminal externe
    ----------------------------------
    FreeFem++ "C:\Users\julien\Desktop\scikit\fr\physiques\darcy\FreeFem\02_darcy_evolution.edp" -ns
*/
// Géométrie : cavité rectangulaire
real largeur = 1;
real hauteur = 5;
mesh Th = square(10, 100, [largeur*x, hauteur*y]);
plot(Th, cmm="cavite", wait=true);

// Definition du cadre variationnel
fespace Qh(Th, P1);
Qh ph, qh;

fespace Vh(Th, P1b);
Vh uh, vh;

fespace Wh(Th, P2);
Wh phih;

/*
    Fluide non-homogene : mu = mu(x,y)
    Matériau non-homogene anisotrope :
        K = [ [ k11(x,y), k12(x,y) ],
              [ k21(x,y), k22(x,y)] ] 
*/
func mu  = 1;
func k11 = 1;
func k12 = 0;
func k21 = 0;
func k22 = 1;

// Probleme variationnel
solve darcy(ph, qh) = int2d(Th)( 1/mu * (k11*dx(ph)+k12*dy(ph))*dx(qh) + (k21*dx(ph)+k22*dy(ph))*dy(qh) ) + on(1, ph=1) + on(3, ph=0);

uh = -dx(ph);
vh = -dy(ph);

// Initialisation de la ligne de front
func phi0 = y < hauteur/10 ? 1 : 0;

phih = phi0;

real  t = 0;
real tf = 50;
real dt = 0.1;

for(int i=0; i < tf/dt; i++){
    t += dt;
    phih = convect([uh, vh], -dt, phih);
    plot(phih, cmm="Front a t = "+t, fill=true, value=true, wait=true);
}