/*
    On poursuit en testant la resolution de notre probleme de poisson sur une 
    grille fine. On reprend le probleme modele et on le resoud par algorithme 
    GMRES. Resultats sur Intel(R) Core(TM) i7-8750H CPU @2.20Ghz.

    |------------------------------|---------------------|
    | Nombre de points sur le bord | Temps de calcul (s) |
    |------------------------------|---------------------|
    |                          128 | 0.061               |
    |                          256 | 0.197               |
    |                          512 | 3.589               |
    |                         1024 | 83.429 ~ 1 min 23   |
    |                         2048 | 1023.48 ~ 17 min 05 |
    |------------------------------|---------------------|

    Dans le prochain script, on utilise le meme cas test sur la parallelisation.

    auteur : Julien VALENTIN
    date   : 10 mai 2022

    execution dans un terminal externe
    ----------------------------------
    FreeFem++ "C:\Users\julien\Documents\GitHub\Scikit\fr\edp\poisson\FreeFem\04_poisson2d_resolution.edp" -v 0
*/

// Solution exacte
func u = 1 - x^2 - y^2;

// Second membre
func f = 4;

// Discretisation du bord du disque
int npoints = 2048;

border C(t=0, 1){
    x = cos(2*pi*t);
    y = sin(2*pi*t);
    label = 1;
}

// Maillage du disque
mesh Th = buildmesh(C(npoints));

// Definition du cadre fonctionnel
fespace Vh(Th, P1);
Vh uh, vh;

real tic = clock();

// Resolution du probleme variationnel
solve poisson2d(uh, vh, solver=GMRES) = int2d(Th)( dx(uh)*dx(vh) + dy(uh)*dy(vh) ) - int2d(Th)( f*vh ) + on(1, uh=0);

real tac = clock();
cout << "Temps de calcul " << tac-tic << " s" << endl;

/////////
// FIN //
/////////